model {
        for(i in 1:N1) {
				Imeana[i] <- log(q * K*P[i]);
				Imeanb[i] <- log(q2* K*P[i]);
				Imeanc[i] <- log(q3* K*P[i]);

				#partion based on area -- assuming single Q
	
				IRV[i] ~ dlnorm(Imeana[i],itau2a);
				INS[i] ~ dlnorm(Imeanb[i],itau2b);
				INA[i] ~ dlnorm(Imeanc[i],itau2c);
				
                 B[i] <- K*P[i] 
                 }
 
 for(i in (N1+1):N) {
				Imeana[i] <- log(q*K*P[i]);
				Imeanb[i] <- log(q2*K*P[i]);
				Imeanc[i] <- log(q3*K*P[i]);
				Imeand[i] <- log(q4*K*P[i]);

				
				IRV[i] ~ dlnorm(Imeana[i],itau2a);
				INS[i] ~ dlnorm(Imeanb[i],itau2b);
				INA[i] ~ dlnorm(Imeanc[i],itau2c);
				ING[i] ~ dlnorm(Imeand[i],itau2d);

                 B[i] <- K*P[i] 
                 }
               
#Process equation
	#P[1]
	
	Pmean[1] <- log(P0)	;
	P[1] ~ dlnorm(Pmean[1],isigma2)
	P.res[1] <- log(P[1])-Pmean[1]
	
	#P[2:N]
	for (i in 2:N) {
		    Pmean[i] <- log(max(P[i-1] + r*P[i-1]*(1-P[i-1]) - k*C[i-1],0.0000001));
		    P[i] ~ dlnorm(Pmean[i],isigma2);
		    P.res[i] <- log(P[i]) - Pmean[i]
		    }

   for(i in 1:N) {
       F[i] <- -log( max(1 - C[i] / B[i], 0.00001))  
     }
  

#####Prior on r######

		r~dnorm(r.a,r.b);
				
#####Prior on k######
		i.k.b<-1/k.b
		k ~ dlnorm(k.a,i.k.b);
		#k ~ dunif(0.000001,1)
		K <- 1/k;	
		
#Prior on P0
P0 ~dunif(P0.a,P0.b)
		
#####Prior on Q#####
		
		q ~dunif(q.a,q.b)
		q2 ~dunif(q.a,q.b)
		q3 ~dunif(q.a,q.b)
		q4 ~dunif(q.a,q.b)
				
#######Priors on isigma2 and itau2#####
	
		sd.p ~ dunif(0.01,7)
		sd.oa ~ dunif(0.01,7)
		sd.ob ~ dunif(0.01,7)
		sd.oc ~ dunif(0.01,7)
		sd.od ~ dunif(0.01,7)
		

		isigma2 <- pow(sd.p,-2)
		itau2a    <- pow(sd.oa,-2)
		itau2b    <- pow(sd.ob,-2)
		itau2c    <- pow(sd.oc,-2)
		itau2d    <- pow(sd.od,-2)
			
	}
