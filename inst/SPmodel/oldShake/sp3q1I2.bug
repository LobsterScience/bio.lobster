#State-space surplus production model with lognormal errors
#I index of abund
#P = B/K

#############################################################################
model surplusprod {

#Observation equation
	
		for(i in 1:3) {
				Imean[i] <- log(q1*K*P[i]);
                 I1[i] ~ dlnorm(Imean[i],itau2);
                 B[i] <- K*P[i] 
                 }
       
      for(i in 4:N) {
				Imean[i] <- log(q1*K*P[i]);
                 I1[i] ~ dlnorm(Imean[i],itau2);
                 I2[i] ~ dlnorm(Imean[i],igam2);
                 B[i] <- K*P[i] 
                 }
                            

#Process equation
	#P[1]
	Pmean[1] ~ dlnorm(0,1) I(0.00001,1.5)	;
		P[1] ~ dlnorm(Pmean[1],isigma2)I(0,1.5)  
		P.res[1] <- log(P[1])-Pmean[1]
	
	#P[2:N]
	for (i in 2:N) {
		    Pmean[i] <- log(max(P[i-1] + r*P[i-1]*(1-P[i-1]) - k*C[i-1],0.01));
		    P[i] ~ dlnorm(Pmean[i],isigma2);
		    P.res[i] <- log(P[i]) - Pmean[i]
		    }
    
#####Prior on r######

		i.r.b<-1/r.b 
		r~dnorm(r.a,i.r.b) I(0.01,2);

#####Prior on k######
		i.k.b<-1/k.b
		k ~ dlnorm(k.a,i.k.b);
		K <- 1/k;	
		
#Reference point calcs	
		#deterministic
		BMSP <- K/2
		MSP<-r*K/4
		FMSP<-r/2
		
		#stochastic from taylor expansion results from bousquet et al. 2008
		
		MSPc<-r*K/4*(1-Sigma2/(r*pow((1-r/4),2))+(4*pow(Sigma2,2)/(pow(r,2)*pow((4-r),4)))*(pow(r,4)-pow(4*r,3)-pow(12*r,2)+48*r-16));
		FMSPc<-r/2-(2*(2-r)*Sigma2)/pow((4-r),2)
		BMSPc <- K/2*(1-(8*Sigma2)/(r*pow(4-r,2))-(8*pow(Sigma2,2)/(r*pow((4-r),5)))*(pow(3*r,3)-pow(18*r,2)-(12*r)+32))
		
#####Prior on Q#####
		#q set to 1 for this exercise
		q1 ~dunif(q.a,q.b)
		


#######Priors on isigma2 and itau2#####
	junk<-a0+b0+c0+d0
		sd.p ~ dunif(0.25,1)
		sd.o1 ~ dunif(0.25,1)
		sd.o2 ~ dunif(0.25,1)
		isigma2 <- pow(sd.p,-2)
		itau2    <- pow(sd.o1,-2)
		igam2    <- pow(sd.o2,-2)
		Sigma2<-1/isigma2; 
		Tau2<-1/itau2;
		Gam2 <- 1/igam2;

#######Project biomass into future#####
	#	for (i in N:(N+nyrs-1)) {C[i]<-8}
		for (i in (N+1):(N+nyrs)) {
		    Pmean[i] <- log(max(P[i-1] + r*P[i-1]*(1-P[i-1]) - k*C[i-1],0.01));
		    P[i] ~ dlnorm(Pmean[i],isigma2);
		    B[i] <- K*P[i] }

	}
